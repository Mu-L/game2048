buildscript {
    repositories {
		google();
		mavenCentral()
	}
    dependencies {
		classpath 'com.android.tools.build:gradle:7.0.3';
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.6.0-RC2' }

}

plugins {
  id 'com.github.triplet.play' version '2.7.5'
}

repositories {
    mavenLocal()
	mavenCentral()
    google()
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
	adbOptions {
		installOptions = ["-r"]
		timeOutInMs = 30 * 1000
	}
    lintOptions {
        disable("GradleCompatible")
    }
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += "-Xmulti-platform"
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/LGPL*'
        exclude 'META-INF/AL2.0'
        exclude 'META-INF/*.kotlin_module'
        exclude '**/*.kotlin_metadata'
        exclude '**/*.kotlin_builtins'
    }
    compileSdkVersion 30
    defaultConfig {
        applicationId 'org.andstatus.game2048'
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 33
        versionName '1.11.0'

        archivesBaseName = "game2048-android-$versionName"
    }
    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
        }
    }
    sourceSets {
        main {
            manifest.srcFile '../src/main/AndroidManifest.xml'
            java.srcDirs += [
                    "../src/main/java",
                    "../src/commonMain/kotlin",
                    "../src/androidMain/kotlin"]
            assets.srcDirs += [
                    "../src/commonMain/resources",
                    "../build/korgeProcessedResources/metadata/main"]
            res.srcDirs  += [
                    "../src/commonMain/resources/res"]
        }
    }
}

if (project.hasProperty("andstatus.google-play-publisher")
        && new File(project.property("andstatus.google-play-publisher").toString() + ".gradle").exists()) {
    apply from: project.property("andstatus.google-play-publisher") + ".gradle";
} else {
    play {
        enabled = false
    }
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.6.0-RC2'
    implementation 'com.soywiz.korlibs.klock:klock-android:2.4.8'
    implementation 'com.soywiz.korlibs.kmem:kmem-android:2.4.8'
    implementation 'com.soywiz.korlibs.kds:kds-android:2.4.8'
    implementation 'com.soywiz.korlibs.krypto:krypto-android:2.4.8'
    implementation 'com.soywiz.korlibs.korge2:korge-android:2.4.8'
    implementation 'com.soywiz.korlibs.korma:korma-android:2.4.8'
    implementation 'com.soywiz.korlibs.korio:korio-android:2.4.8'
    implementation 'com.soywiz.korlibs.korim:korim-android:2.4.8'
    implementation 'com.soywiz.korlibs.korau:korau-android:2.4.8'
    implementation 'com.soywiz.korlibs.korgw:korgw-android:2.4.8'
    implementation 'com.soywiz.korlibs.korinject:korinject-android:2.4.8'
    implementation 'com.soywiz.korlibs.klogger:klogger-android:2.4.8'
    implementation 'com.soywiz.korlibs.korte:korte-android:2.4.8'
}
