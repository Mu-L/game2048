buildscript {
    repositories {
		google();
		mavenCentral()
	}
    dependencies {
		classpath 'com.android.tools.build:gradle:7.1.1';
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.6.10' }

}

plugins {
  id 'com.github.triplet.play' version '2.7.5'
}

repositories {
    mavenLocal()
	mavenCentral()
    google()
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
    installation {
        installOptions '-r'
        timeOutInMs 30 * 1000
    }
    lint {
        disable 'GradleCompatible'
    }
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += "-Xmulti-platform"
    }
    packagingOptions {
        jniLibs {
            excludes += ['META-INF/LGPL*']
        }
        resources {
            excludes += [
                'META-INF/DEPENDENCIES',
                'META-INF/LICENSE',
                'META-INF/LICENSE.txt',
                'META-INF/license.txt',
                'META-INF/NOTICE',
                'META-INF/NOTICE.txt',
                'META-INF/notice.txt',
                'META-INF/LGPL*',
                'META-INF/AL2.0',
                'META-INF/*.kotlin_module',
                '**/*.kotlin_metadata',
                '**/*.kotlin_builtins'
            ]
        }
    }
    compileSdkVersion 30
    defaultConfig {
        applicationId 'org.andstatus.game2048'
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 33
        versionName '1.11.0'

        archivesBaseName = "game2048-android-$versionName"
    }
    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
        }
    }
    sourceSets {
        main {
            manifest.srcFile '../src/main/AndroidManifest.xml'
            java.srcDirs += [
                    "../src/main/java",
                    "../src/commonMain/kotlin",
                    "../src/androidMain/kotlin"]
            assets.srcDirs += [
                    "../src/commonMain/resources",
                    "../build/korgeProcessedResources/metadata/main"]
            res.srcDirs  += [
                    "../src/commonMain/resources/res"]
        }
    }
}

if (project.hasProperty("andstatus.google-play-publisher")
        && new File(project.property("andstatus.google-play-publisher").toString() + ".gradle").exists()) {
    apply from: project.property("andstatus.google-play-publisher") + ".gradle";
} else {
    play {
        enabled = false
    }
}

dependencies {
    def korgeVersion = '2.5.2'

    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.6.10'
    implementation "com.soywiz.korlibs.klock:klock-android:$korgeVersion"
    implementation "com.soywiz.korlibs.kmem:kmem-android:$korgeVersion"
    implementation "com.soywiz.korlibs.kds:kds-android:$korgeVersion"
    implementation "com.soywiz.korlibs.krypto:krypto-android:$korgeVersion"
    implementation "com.soywiz.korlibs.korge2:korge-android:$korgeVersion"
    implementation "com.soywiz.korlibs.korma:korma-android:$korgeVersion"
    implementation "com.soywiz.korlibs.korio:korio-android:$korgeVersion"
    implementation "com.soywiz.korlibs.korim:korim-android:$korgeVersion"
    implementation "com.soywiz.korlibs.korau:korau-android:$korgeVersion"
    implementation "com.soywiz.korlibs.korgw:korgw-android:$korgeVersion"
    implementation "com.soywiz.korlibs.korinject:korinject-android:$korgeVersion"
    implementation "com.soywiz.korlibs.klogger:klogger-android:$korgeVersion"
    implementation "com.soywiz.korlibs.korte:korte-android:$korgeVersion"
}
